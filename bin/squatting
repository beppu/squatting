#!/usr/bin/perl

use strict;
no  strict 'refs';
use warnings;

use Getopt::Long;
use File::Basename;

# defaults
$_{port}  = 4234;
$_{debug} = 1;

# command line options
GetOptions(
  \%_,
  'port|p=i',
  'log|l=s',
  'console|C',
  'debug=i',
  'help',
  'version|v',
);

sub help {
  print "((( Start a Squatting Application )))

Usage: squatting [OPTIONS]... App

Options:
    -p, --port NUM                   Port for web server (defaults to $_{port})
    -l, --log FILE                   Log file
    -C, --console                    Run in console mode with Shell::Perl
    -v, --version                    Display Squatting version
    --debug NUM                      Continuity debug_level (defaults to $_{debug})
    --help                           Display this help message
";
}

if ($_{version}) {
  print "Version $Squatting::VERSION\n";
  exit 0;
} elsif ($_{help}) {
  help;
  exit 0;
} elsif ($_{log}) {

} else {

}

my $module = shift;
if ($module) {
  require "$module.pm";
  my $app = basename $module;
  if ($_{console}) {
    eval { require Shell::Perl };
    if ($@) {
      print "Please install Shell::Perl if you'd like to use the Squatting console.\n";
      exit 1;
    }
    $app->init;
    sub Squatting::Controller::mock_init {
      my $self = shift;
      $self->{cr}          = {};
      $self->{env}         = { %ENV, REQUEST_PATH => Squatting::R($self->name, @_) };
      $self->{cookies}     = {};
      $self->{input}       = {};
      $self->{set_cookies} = {};
      $self->{headers}     = {};
      $self->{v}           = {};
      $self->{status}      = 200;
      $self;
    };
    foreach my $method qw(get post) {
      *{$app."::$method" } = sub { 
        my $cc = ${$app."::Controllers::C"}{$_[1]}->clone->mock_init(@_[2..$#_]);
        my $content = $cc->$method(@_[2..$#_]);
        ($cc, $content);
      };
    }
    $0 = $module;
    my $pirl = Shell::Perl->new;
    $pirl->set_package($module."::Controllers");
    $pirl->run;
  } else {
    $app->go(
      port        => $_{port},
      log         => $_{log},
      debug_level => $_{debug},
    );
  }
} else {
  help;
}

exit 0;

__END__

=head1 NAME

squatting -- start up a Squatting server

=head1 SYNOPSIS

Usage

  squatting [OPTION]... APPLICATION

Example:  Starting Bavl on port 4234

  squatting -p 4234 Bavl

=head1 DESCRIPTION

We're here to stay.

=head1 OPTIONS

=over 2

=item -h, --host HOSTNAME

TODO - Host for web server to bind to (default is all IPs)

=item -p, --port NUM

TODO - Port for web server (defaults to 4234)

=item -l, --log FILE

TODO - Start a log file ('-' for STDOUT)

=item -C, --console

TODO - Run in console mode with pirl

=item -?, --help

TODO - Show the help message

=item -v, --version

TODO - Show version

=back

=cut

# Local Variables: ***
# mode: cperl ***
# indent-tabs-mode: t ***
# cperl-close-paren-offset: -2 ***
# cperl-continued-statement-offset: 2 ***
# cperl-indent-level: 2 ***
# cperl-indent-parens-as-block: t ***
# cperl-tab-always-indent: f ***
# End: ***
# vim:tabstop=2 softtabstop=2 shiftwidth=2 shiftround expandtab
